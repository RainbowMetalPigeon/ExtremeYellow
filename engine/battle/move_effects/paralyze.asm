ParalyzeEffect_:
	ld hl, wEnemyMonStatus
	ld de, wPlayerMoveType
	ldh a, [hWhoseTurn]
	and a
	jp z, .next
	ld hl, wBattleMonStatus
	ld de, wEnemyMoveType
.next
	ld a, [hl]
	and a ; does the target already have a status ailment?
	jr nz, .didntAffect
; new to check terrain
	push hl
	call CheckIfNonTurnPokemonIsFlying ; z flag = FLYING
	pop hl
	jr z, .noMistyTerrain
	CheckEvent EVENT_TERRAIN_MISTY
	jr z, .noMistyTerrain
	ld c, 50
	call DelayFrames
	ld hl, TheTerrainPreventsText
	jp PrintText
.noMistyTerrain
; BTV
; check if the target is immune due to types
; new, during an inverse battle ELECTRIC is not non-effective vs GROUND
	ld a, [wInverseBattle]
	cp 1
	jr z, .hitTest
; back to vanilla
	ld a, [de]
	cp ELECTRIC
	jr nz, .hitTest
	ld b, h
	ld c, l
	inc bc
	ld a, [bc]
	cp GROUND
	jr z, .doesntAffect
	inc bc
	ld a, [bc]
	cp GROUND
	jr z, .doesntAffect
.hitTest
	push hl
	callfar MoveHitTest
	pop hl
	ld a, [wMoveMissed]
	and a
	jr nz, .didntAffect
	set PAR, [hl]
	callfar QuarterSpeedDueToParalysis
	ld c, 3 ; edited, was 30
	call DelayFrames
	callfar PlayCurrentMoveAnimation
	jpfar PrintMayNotAttackText
.didntAffect
	ld c, 5 ; edited, was 50
	call DelayFrames
	jpfar PrintDidntAffectText
.doesntAffect
	ld c, 5 ; edited, was 50
	call DelayFrames
	jpfar PrintDoesntAffectText
